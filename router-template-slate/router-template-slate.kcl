// Router template for a slate
// A guide for routing a slate for a cross bar.

// Define constants
routerDiameter = 12.7 // mm
mmInInch = 25.4 // mm
templateDiameter = mmInInch * 11 / 16 // mm
templateGap = ((templateDiameter - routerDiameter) / 2) -0.5 // mm
slateWidthHalf = 41.5 / 2 // mm
minClampingDistance = 50 + 30 // mm
templateThickness = 10 // mm
radius = 10 // mm
depth = 30 // mm
length001 = slateWidthHalf - radius // mm
length002 = depth + minClampingDistance // mm

// Create the first sketch
sketch001 = startSketchOn('XZ')
  |> startProfileAt([0, depth - templateGap], %)
  |> xLine(length001, %, $seg01)
  |> arc({
       angleEnd: 0,
       angleStart: 90,
       radius: radius - templateGap
     }, %)
  |> yLineTo(-templateGap * 2 - (templateDiameter / 2), %, $seg05)
  |> xLineTo(slateWidthHalf + templateThickness, %, $seg04)
  |> yLine(-length002, %, $seg03)
  |> xLineTo(ZERO, %, $seg02)
  // |> line([7.78, 11.16], %)
  |> xLine(-segLen(seg02), %)
  |> yLine(segLen(seg03), %)
  |> xLine(segLen(seg04), %)
  |> yLine(segLen(seg05), %)
  |> arc({
       angleEnd: 90,
       angleStart: 180,
       radius: radius - templateGap
     }, %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)

// Extrude the first sketch
extrude001 = extrude(5, sketch001)

// Create the second sketch
sketch002 = startSketchOn(extrude001, 'START')
  |> startProfileAt([
       -slateWidthHalf,
       -templateGap * 2 - (templateDiameter / 2)
     ], %)
  |> xLine(-7, %, $rectangleSegmentA001)
  |> angledLine([
       segAng(rectangleSegmentA001) + 90,
       minClampingDistance
     ], %, $rectangleSegmentB001)
  |> angledLine([
       segAng(rectangleSegmentA001),
       -segLen(rectangleSegmentA001)
     ], %, $rectangleSegmentC001)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)

// Extrude the second sketch
extrude002 = extrude(7.5, sketch002)

// Create the third sketch
sketch003 = startSketchOn(extrude001, 'START')
  |> startProfileAt([
       slateWidthHalf,
       -templateGap * 2 - (templateDiameter / 2)
     ], %)
  |> xLine(7, %, $rectangleSegmentA002)
  |> angledLine([
       segAng(rectangleSegmentA002) - 90,
       minClampingDistance
     ], %)
  |> angledLine([
       segAng(rectangleSegmentA002),
       -segLen(rectangleSegmentA002)
     ], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)

// Extrude the third Sketch
extrude003 = extrude(7.5, sketch003)
