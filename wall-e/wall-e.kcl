// WALL-E
// An inquisitive trash compacting robot imagined by Disney Pixar Animation Studios

// Right Side Body Surfaces
sketch001 = startSketchOn(offsetPlane('XY', 8))
  |> startProfileAt([-9, 9], %)
  |> angledLine([0, 18], %, $rectangleSegmentA001)
  |> angledLine([segAng(rectangleSegmentA001) + 90, -17], %, $rectangleSegmentB001)
  |> angledLine([
       segAng(rectangleSegmentA001),
       -segLen(rectangleSegmentA001)
     ], %, $rectangleSegmentC001)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(.5, %)
sketch002 = startSketchOn(offsetPlane('-YZ', 8.5))
  |> startProfileAt([-8, 8.5], %)
  |> angledLine([0, 17], %, $rectangleSegmentA002)
  |> line([-1, 8.5], %)
  |> line([1, 8.5], %, $rectangleSegmentB002)
  |> angledLine([
       segAng(rectangleSegmentA002),
       -segLen(rectangleSegmentA002)
     ], %, $rectangleSegmentC002)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(.5, %)

// Right Arm Channel
sketch003 = startSketchOn(sketch002, 'END')
  |> startProfileAt([5.5, 9], %)
  |> xLine(1.5, %)
  |> yLine(14, %)
  |> xLine(-13, %)
  |> yLine(-1.51, %)
  |> xLine(11.5, %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(-.1, %)

// Left Side Body Surfaces
sketch004 = startSketchOn(offsetPlane('YZ', 8.5))
  |> startProfileAt([-8, 8.5], %)
  |> angledLine([0, 17], %, $rectangleSegmentA003)
  |> line([-1, 8.5], %)
  |> line([1, 8.5], %, $rectangleSegmentB003)
  |> angledLine([
       segAng(rectangleSegmentA003),
       -segLen(rectangleSegmentA003)
     ], %, $rectangleSegmentC003)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(.5, %)

// Left Arm Channel
sketch005 = startSketchOn(sketch004, 'END')
  |> startProfileAt([-5.5, 9], %)
  |> xLine(-1.5, %)
  |> yLine(14, %)
  |> xLine(13, %)
  |> yLine(-1.51, %)
  |> xLine(-11.5, %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(-.1, %)
sketch006 = startSketchOn(offsetPlane('XZ', 7.5))
  |> startProfileAt([-8.5, 17 + 8.5], %)
  |> angledLine([0, 17], %, $rectangleSegmentA004)
  |> angledLine([
       segAng(rectangleSegmentA004) + 90,
       -5.5
     ], %, $rectangleSegmentB004)
  |> angledLine([
       segAng(rectangleSegmentA004),
       -segLen(rectangleSegmentA004)
     ], %, $rectangleSegmentC004)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(.5, %)

// Front Face Details
sketch007 = startSketchOn(sketch006, 'END')
  |> startProfileAt([-9, 24.4], %)
  |> angledLine([0, 18], %, $rectangleSegmentA007)
  |> angledLine([segAng(rectangleSegmentA007) - 90, 0.3], %, $rectangleSegmentB007)
  |> angledLine([
       segAng(rectangleSegmentA007),
       -segLen(rectangleSegmentA007)
     ], %, $rectangleSegmentC007)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(.1, %)
sketch008 = startSketchOn(sketch006, 'END')
  |> startProfileAt([-9, 22], %)
  |> angledLine([0, 18], %, $rectangleSegmentA008)
  |> angledLine([segAng(rectangleSegmentA008) - 90, 0.3], %, $rectangleSegmentB008)
  |> angledLine([
       segAng(rectangleSegmentA008),
       -segLen(rectangleSegmentA008)
     ], %, $rectangleSegmentC008)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(.1, %)

// Central Display and Controls
sketch009 = startSketchOn(sketch006, 'END')
  |> startProfileAt([-4, 20.35], %)
  |> angledLine([0, 8], %, $rectangleSegmentA010)
  |> angledLine([
       segAng(rectangleSegmentA010) + 90,
       5.15
     ], %, $rectangleSegmentB010)
  |> angledLine([
       segAng(rectangleSegmentA010),
       -segLen(rectangleSegmentA010)
     ], %, $rectangleSegmentC010)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(1, %)
sketch010 = startSketchOn(sketch009, 'END')
  |> startProfileAt([3.79, 20.59], %)
  |> angledLine([180, 3.54], %, $rectangleSegmentA011)
  |> angledLine([
       segAng(rectangleSegmentA011) - 90,
       4.68
     ], %, $rectangleSegmentB011)
  |> angledLine([
       segAng(rectangleSegmentA011),
       -segLen(rectangleSegmentA011)
     ], %, $rectangleSegmentC011)
  |> lineTo([profileStartX(%), profileStartY(%)], %, $rectangleSegmentD011)
  |> close(%)
  |> extrude(-.1, %)
  |> chamfer({
       length = .2,
       tags = [
         getNextAdjacentEdge(rectangleSegmentA011),
         getNextAdjacentEdge(rectangleSegmentB011),
         getNextAdjacentEdge(rectangleSegmentC011),
         getNextAdjacentEdge(rectangleSegmentD011)
       ]
     }, %)
sketch011 = startSketchOn(sketch009, 'END')
  |> startProfileAt([-3.55, 25], %)
  |> angledLine([0, 3.33], %, $rectangleSegmentA012)
  |> angledLine([
       segAng(rectangleSegmentA012) - 90,
       1.23
     ], %, $rectangleSegmentB012)
  |> angledLine([
       segAng(rectangleSegmentA012),
       -segLen(rectangleSegmentA012)
     ], %, $rectangleSegmentC012)
  |> lineTo([profileStartX(%), profileStartY(%)], %, $rectangleSegmentD012)
  |> close(%)
  |> extrude(-.05, %)
  |> chamfer({
       length = .2,
       tags = [
         getNextAdjacentEdge(rectangleSegmentA012),
         getNextAdjacentEdge(rectangleSegmentB012),
         getNextAdjacentEdge(rectangleSegmentC012),
         getNextAdjacentEdge(rectangleSegmentD012)
       ]
     }, %)

// Ventilation Grate
sketch012 = startSketchOn(sketch011, 'START')
  |> startProfileAt([-3.33, 24.79], %)
  |> angledLine([0, 0.75], %, $rectangleSegmentA013)
  |> angledLine([
       segAng(rectangleSegmentA013) - 90,
       0.15
     ], %, $rectangleSegmentB013)
  |> angledLine([
       segAng(rectangleSegmentA013),
       -segLen(rectangleSegmentA013)
     ], %, $rectangleSegmentC013)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> patternLinear2d({
       axis = [1, 0],
       instances = 3,
       distance = 1.06
     }, %)
  |> patternLinear2d({
       axis = [0, -1],
       instances = 3,
       distance = .33
     }, %)
  |> extrude(-0.7, %)

// Indicator Light and Indent
sketch013 = startSketchOn(sketch009, 'END')
  |> startProfileAt([-3.55, 23.43], %)
  |> angledLine([0, 1.66], %, $rectangleSegmentA014)
  |> angledLine([
       segAng(rectangleSegmentA014) - 90,
       1.66
     ], %, $rectangleSegmentB014)
  |> angledLine([
       segAng(rectangleSegmentA014),
       -segLen(rectangleSegmentA014)
     ], %, $rectangleSegmentC014)
  |> lineTo([profileStartX(%), profileStartY(%)], %, $rectangleSegmentD014)
  |> close(%)
  |> extrude(-.2, %)
  |> chamfer({
       length = .2,
       tags = [
         getNextAdjacentEdge(rectangleSegmentA014),
         getNextAdjacentEdge(rectangleSegmentB014),
         getNextAdjacentEdge(rectangleSegmentC014),
         getNextAdjacentEdge(rectangleSegmentD014)
       ]
     }, %)
sketch014 = startSketchOn(sketch013, 'START')
  |> circle({
       center = [-3.55 + 1.66 / 2, 23.43 - (1.66 / 2)],
       radius = 0.46
     }, %)
  |> extrude(-.1, %)

// Top Face and Collar
sketch015 = startSketchOn(offsetPlane('XY', 17.5 + 8))
  |> startProfileAt([-9, 9], %)
  |> angledLine([0, 18], %, $rectangleSegmentA005)
  |> angledLine([segAng(rectangleSegmentA005) - 90, 18], %, $rectangleSegmentB005)
  |> angledLine([
       segAng(rectangleSegmentA005),
       -segLen(rectangleSegmentA005)
     ], %, $rectangleSegmentC005)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> hole(  startSketchOn(offsetPlane('XY', 19))
    |> startProfileAt([-1.5, 7], %)
    |> angledLine([0, 3], %, $rectangleSegmentA006)
    |> angledLine([segAng(rectangleSegmentA004) - 90, 6], %, $rectangleSegmentB006)
    |> angledLine([
         segAng(rectangleSegmentA006),
         -segLen(rectangleSegmentA006)
       ], %, $rectangleSegmentC006)
    |> lineTo([profileStartX(%), profileStartY(%)], %)
    |> close(%), %)
  |> extrude(.25, %)

// Rear Trash Chute
sketch016 = startSketchOn(offsetPlane('YZ', 7.75))
  |> startProfileAt([3.75, 17.4 + 8], %)
  |> xLine(5, %)
  |> angledLine({ angle = -55, length = .01 }, %, $seg01)
  |> tangentialArcTo([8, 8.6], %)
  |> xLine(-3, %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(.5, %)
  |> patternLinear3d({
       axis = [-1, 0, 0],
       instances = 2,
       distance = 7.75 * 2 + .5
     }, %)
sketch017 = startSketchOn(offsetPlane('YZ', 7.75))
  |> startProfileAt([6.13, 8.74], %)
  |> yLine(16.46, %)
  |> xLine(2.64, %)
  |> line([0.68, -0.85], %, $seg02)
  |> line([-0.26, -0.13], %, $seg03)
  |> line([-0.42, 0.47], %)
  |> line([0.68, -1.83], %)
  |> line([-0.34, -0.09], %)
  |> line([-0.21, 0.64], %)
  |> xLine(-0.6, %)
  |> yLine(-14.63, %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(-15.5, %)

// Rear Trash Chute Handle
sketch018 = startSketchOn(sketch017, seg03)
  |> startProfileAt([19.5, 0], %)
  |> yLine(2.99, %)
  |> xLine(-1.17, %)
  |> yLine(0.59, %)
  |> xLine(1.83, %)
  |> yLine(-3.58, %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> mirror2d({ axis = 'X' }, %)
  |> extrude(1, %)

// Side Detail
sketch019 = startSketchOn(offsetPlane('YZ', 9))
  |> startProfileAt([-5.5, 8], %)
  |> yLine(10, %)
  |> line([10, -2], %)
  |> lineTo([8.7, profileStartY(%)], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(.1, %)
  |> patternLinear3d({
       axis = [-1, 0, 0],
       instances = 2,
       distance = 18.1
     }, %)
sketch020 = startSketchOn(offsetPlane('YZ', 9))
  |> circle({ center = [6, 18.2], radius = 1.4 }, %)
  |> hole(circle({ center = [6.61, 18.81], radius = 0.2 }, %), %)
  |> extrude(.05, %)
  |> patternLinear3d({
       axis = [-1, 0, 0],
       instances = 2,
       distance = 18.1
     }, %)

// Front Trash Hatch
sketch021 = startSketchOn(offsetPlane('XZ', 8))
  |> startProfileAt([-9, 8.1], %)
  |> angledLine([0, 18], %, $rectangleSegmentA009)
  |> angledLine([
       segAng(rectangleSegmentA009) + 90,
       12.25
     ], %, $rectangleSegmentB009)
  |> angledLine([
       segAng(rectangleSegmentA009),
       -segLen(rectangleSegmentA009)
     ], %, $rectangleSegmentC009)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(1, %)
  |> chamfer({
       length = 0.8,
       tags = [getOppositeEdge(rectangleSegmentA009)]
     }, %)

// Front Hatch Detail
sketch022 = startSketchOn(sketch021, 'END')
  |> startProfileAt([-6, 20], %)
  |> angledLine([0, .5], %, $rectangleSegmentA015)
  |> angledLine([segAng(rectangleSegmentA015) - 90, 8.5], %, $rectangleSegmentB015)
  |> angledLine([
       segAng(rectangleSegmentA015),
       -segLen(rectangleSegmentA015)
     ], %, $rectangleSegmentC015)
  |> lineTo([profileStartX(%), profileStartY(%)], %, $rectangleSegmentD015)
  |> close(%)
  |> extrude(-.25, %)
  |> fillet({
       radius = .1,
       tags = [
         getOppositeEdge(rectangleSegmentB015),
         getOppositeEdge(rectangleSegmentD015)
       ]
     }, %)
sketch023 = startSketchOn(sketch021, 'END')
  |> startProfileAt([-0.25, 20], %)
  |> angledLine([0, 0.5], %, $rectangleSegmentA016)
  |> angledLine([segAng(rectangleSegmentA016) - 90, 8.5], %, $rectangleSegmentB016)
  |> angledLine([
       segAng(rectangleSegmentA016),
       -segLen(rectangleSegmentA016)
     ], %, $rectangleSegmentC016)
  |> lineTo([profileStartX(%), profileStartY(%)], %, $rectangleSegmentD016)
  |> close(%)
  |> extrude(-0.25, %)
  |> fillet({
       radius = .1,
       tags = [
         getOppositeEdge(rectangleSegmentB016),
         getOppositeEdge(rectangleSegmentD016)
       ]
     }, %)

sketch024 = startSketchOn(sketch021, 'END')
  |> startProfileAt([6, 20], %)
  |> angledLine([180, -0.5], %, $rectangleSegmentA017)
  |> angledLine([
       segAng(rectangleSegmentA017) + 90,
       -8.5
     ], %, $rectangleSegmentB017)
  |> angledLine([
       segAng(rectangleSegmentA017),
       -segLen(rectangleSegmentA017)
     ], %, $rectangleSegmentC017)
  |> lineTo([profileStartX(%), profileStartY(%)], %, $rectangleSegmentD017)
  |> close(%)
  |> extrude(-0.25, %)
  |> fillet({
       radius = 0.1,
       tags = [
         getOppositeEdge(rectangleSegmentB017),
         getOppositeEdge(rectangleSegmentD017)
       ]
     }, %)

// Eyes
plane001 = {
  plane = {
    origin = [0, -14, 3.0],
    xAxis = [1, 0, 0],
    yAxis = [0, 0.25, 1.0],
    zAxis = [-1.0, 0, 0]
  }
}

// Use a Shell to Create Left Eye Outer Trim
sketch025 = startSketchOn(plane001)
  |> startProfileAt([.88, 37.76], %)
  |> line([4, -0.7], %)
  |> tangentialArcTo([5.28, 33.23], %)
  |> tangentialArcTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(0.35, %)
shell({
  faces = ['end', 'start'],
  thickness = 0.050
}, sketch025)

// Left Eye Body
sketch026 = startSketchOn(plane001)
  |> startProfileAt([.88, 37.76], %)
  |> line([4, -0.7], %)
  |> tangentialArcTo([5.28, 33.23], %)
  |> tangentialArcTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(-4.5, %)

// Left Eye Lense
sketch027 = startSketchOn(plane001)
  |> circle({ center = [2.55, 35.5], radius = 1.5 }, %)
  |> hole(circle({ center = [2.55, 35.5], radius = 1.45 }, %), %)
  |> extrude(.2, %)
sketch028 = startSketchOn(plane001)
  |> circle({ center = [2.55, 35.5], radius = 1.45 }, %)
  |> hole(circle({
       center = [2.55, 35.5],
       radius = 1.45 - .45
     }, %), %)
  |> extrude(.14, %)
sketch029 = startSketchOn(plane001)
  |> circle({ center = [2.55, 35.5], radius = 0.9 }, %)
  |> extrude(.15, %)

// Left Eye Details
sketch030 = startSketchOn(plane001)
  |> circle({ center = [1.3, 37.2], radius = .2 }, %)
  |> hole(circle({ center = [1.3, 37.2], radius = .15 }, %), %)
  |> extrude(.05, %)
sketch031 = startSketchOn(plane001)
  |> circle({ center = [4.9, 36.5], radius = .15 }, %)
  |> hole(circle({ center = [4.9, 36.5], radius = .1 }, %), %)
  |> extrude(.05, %)
sketch032 = startSketchOn(plane001)
  |> circle({ center = [4.4, 33.4], radius = .15 }, %)
  |> hole(circle({ center = [4.4, 33.4], radius = .1 }, %), %)
  |> extrude(.05, %)

// Left Eye Body
sketch033 = startSketchOn(sketch026, 'start')
  |> startProfileAt([-.97, 40.4 - 4], %)
  |> angledLine({ angle = 10.5, length = -4.7 }, %)
  |> angledLine({ angle = 10.5 - 90, length = .25 }, %)
  |> angledLine({ angle = 10.5, length = 4.7 }, %)
  |> close(%)
  |> extrude(4.5, %)
sketch034 = startSketchOn(sketch026, 'start')
  |> startProfileAt([-0.69, 39.24 - 4], %)
  |> xLine(1.39, %)
  |> line([-0.01, -0.16], %)
  |> tangentialArcTo([-0.66, 38.9 - 4], %)
  |> line([-0.17, 0.01], %)
  |> line([-0.65, -1.21], %)
  |> line([-3.43, -0.78], %)
  |> line([-0.46, 2.39], %, $seg03E)
  |> line([4.35, 0.8], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(4.5, %)
sketch035 = startSketchOn(sketch034, seg03E)
  |> startProfileAt([-33.25, -9.75], %)
  |> yLine(4.45, %, $seg01E)
  |> xLine(-1.9, %)
  |> yLine(-1.5, %)
  |> xLine(0.8, %)
  |> yLineTo(profileStartY(%), %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(.15, %)
  |> fillet({
       radius = 0.12,
       tags = [getOppositeEdge(seg01E)]
     }, %)

// Use a Shell to Create Right Eye Outer Trim
sketch036 = startSketchOn(plane001)
  |> startProfileAt([-.88, 37.76], %)
  |> line([-4, -0.7], %)
  |> tangentialArcTo([-5.28, 33.23], %)
  |> tangentialArcTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(0.35, %)
shell({
  faces = ['end', 'start'],
  thickness = 0.050
}, sketch036)

// Right Eye Body
sketch037 = startSketchOn(plane001)
  |> startProfileAt([-.88, 37.76], %)
  |> line([-4, -0.7], %)
  |> tangentialArcTo([-5.28, 33.23], %)
  |> tangentialArcTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(-4.5, %)

// Right Eye Lense
sketch038 = startSketchOn(plane001)
  |> circle({ center = [-2.55, 35.5], radius = 1.5 }, %)
  |> hole(circle({
       center = [-2.55, 35.5],
       radius = 1.45
     }, %), %)
  |> extrude(.2, %)
sketch039 = startSketchOn(plane001)
  |> circle({
       center = [-2.55, 35.5],
       radius = 1.45
     }, %)
  |> hole(circle({
       center = [-2.55, 35.5],
       radius = 1.45 - .45
     }, %), %)
  |> extrude(.14, %)
sketch040 = startSketchOn(plane001)
  |> circle({ center = [-2.55, 35.5], radius = 0.9 }, %)
  |> extrude(.15, %)
sketch041 = startSketchOn(plane001)
  |> circle({ center = [-1.3, 37.2], radius = .2 }, %)
  |> hole(circle({ center = [-1.3, 37.2], radius = .15 }, %), %)
  |> extrude(.05, %)

// Right Eye Details
sketch042 = startSketchOn(plane001)
  |> circle({ center = [-4.9, 36.5], radius = .15 }, %)
  |> hole(circle({ center = [-4.9, 36.5], radius = .1 }, %), %)
  |> extrude(.05, %)
sketch043 = startSketchOn(plane001)
  |> circle({ center = [-4.4, 33.4], radius = .15 }, %)
  |> hole(circle({ center = [-4.4, 33.4], radius = .1 }, %), %)
  |> extrude(.05, %)

// Right Eye Body
sketch044 = startSketchOn(sketch037, 'start')
  |> startProfileAt([.97, 40.4 - 4], %)
  |> angledLine({ angle = -10.5, length = 4.7 }, %)
  |> angledLine({ angle = -10.5 + 90, length = -.25 }, %)
  |> angledLine({ angle = -10.5, length = -4.7 }, %)
  |> close(%)
  |> extrude(4.5, %)
sketch045 = startSketchOn(sketch037, 'start')
  |> startProfileAt([0.68, 39.27 - 4], %)
  |> xLine(-1.39, %)
  |> line([0.02, 0.21], %)
  |> tangentialArcTo([0.68, 39.56 - 4], %)
  |> line([0.35, 0.55], %)
  |> line([4.39, -0.81], %)
  |> line([-0.34, -2.2], %, $seg04E)
  |> line([-3.7, 0.74], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(4.5, %)
sketch046 = startSketchOn(sketch045, seg04E)
  |> startProfileAt([33.49, -9.75], %)
  |> yLine(4.45, %, $seg02E)
  |> xLine(1.9, %)
  |> yLine(-1.5, %)
  |> xLine(-0.8, %)
  |> yLineTo(profileStartY(%), %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(.15, %)
  |> fillet({
       radius = 0.12,
       tags = [getOppositeEdge(seg02E)]
     }, %)

// Shoulder Coupling
sketch047 = startSketchOn(offsetPlane('YZ', 1.4))
  |> startProfileAt([3.25, 17.25], %)
  |> yLine(9, %)
  |> tangentialArcToRelative([1.5, 0], %)
  |> yLine(-9, %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(-.25, %)
sketch048 = startSketchOn(offsetPlane('YZ', -1.4))
  |> startProfileAt([3.25, 17.25], %)
  |> yLine(9, %)
  |> tangentialArcToRelative([1.5, 0], %)
  |> yLine(-9, %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(.25, %)
sketch049 = startSketchOn(offsetPlane('YZ', -1.41))
  |> circle({ center = [4, 26.28], radius = 0.34 }, %)
  |> extrude(3.02 - .2, %)

// Neck
plane002 = {
  plane = {
    origin = [0, 4, 26.28],
    xAxis = [1, 0, 0],
    yAxis = [0, 0.855, 1.0],
    zAxis = [-1.0, 0, 0]
  }
}

// Teloscoping Neck Strut
sketch050 = startSketchOn(plane002)
  |> circle({ center = [0, 0], radius = 1.15 }, %)
  |> extrude(3, %)
sketch051 = startSketchOn(plane002)
  |> circle({ center = [0, 0], radius = 1.15 }, %)
  |> extrude(-1, %)
sketch052 = startSketchOn(plane002)
  |> circle({ center = [0, 0], radius = .99 }, %)
  |> extrude(4.5, %)
sketch053 = startSketchOn(plane002)
  |> circle({ center = [0, 0], radius = .65 }, %)
  |> extrude(7.5, %)

// Neck Pivot Joint
sketch054 = startSketchOn(offsetPlane('YZ', .15))
  |> circle({ center = [-1.8, 31.2], radius = 1.28 }, %)
  |> extrude(1.2, %)
sketch055 = startSketchOn(offsetPlane('YZ', -.15))
  |> circle({ center = [-1.8, 31.2], radius = 1.28 }, %)
  |> extrude(-1.2, %)

// Neck Mount
sketch056 = startSketchOn(offsetPlane('YZ', -1))
  |> startProfileAt([-3.15, 31.66], %)
  |> line([0.31, 1.02], %)
  |> line([2.35, 1.14], %)
  |> line([1.79, -0.51], %, $seg01N)
  |> line([-0.23, -0.99], %)
  |> line([-3.03, -1.11], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(2, %)
sketch057 = startSketchOn(sketch056, seg01N)
  |> circle({ center = [8.7, 0], radius = 0.42 }, %)
  |> extrude(.1, %)
sketch001N = startSketchOn(sketch057, 'END')
  |> circle({ center = [8.7, 0], radius = 0.75 }, %)
  |> extrude(.5, %)
sketch058 = startSketchOn(sketch001N, 'END')
  |> circle({ center = [8.7, 0], radius = 0.36 }, %)
  |> extrude(2, %)

// Function to Create Treads
fn tread = (width) => {
  // Draw Tread Perimeter and Shell for Thickness
  sketch059 = startSketchOn(offsetPlane('YZ', width))
    |> startProfileAt([2.34, 16.11], %)
    |> angledLineToY({ angle = 45, to = 0 }, %, $seg04)
    |> xLineTo(14, %, $seg05)
    |> lineTo([profileStartX(%), profileStartY(%)], %, $seg06)
    |> close(%)
    |> extrude(6.7, %)
    |> fillet({
         radius = 7.1,
         tags = [getNextAdjacentEdge(seg04)]
       }, %)
    |> fillet({
         radius = 8,
         tags = [getNextAdjacentEdge(seg05)]
       }, %)
    |> fillet({
         radius = 2.5,
         tags = [getNextAdjacentEdge(seg06)]
       }, %)
  shell({
    faces = ['END', 'START'],
    thickness = 0.35
  }, sketch059)

  // Wheels within Tread
  sketch060 = startSketchOn(offsetPlane('YZ', width + .5))
    |> circle({ center = [-6.9, 2.85], radius = 2.1 }, %)
    |> hole(circle({ center = [-6.9, 2.85], radius = 1.7 }, %), %)
    |> extrude(5.7, %)
  sketch061 = startSketchOn(offsetPlane('YZ', width + .5))
    |> circle({ center = [-2, 7.9], radius = 2.1 }, %)
    |> hole(circle({ center = [-2, 7.9], radius = 1.7 }, %), %)
    |> extrude(6, %)
  sketch062 = startSketchOn(offsetPlane('YZ', width + .5))
    |> circle({
         center = [5.85, 4.15],
         radius = 7.275 / 2
       }, %)
    |> extrude(6, %)
  sketch063 = startSketchOn(offsetPlane('YZ', width + .5))
    |> circle({ center = [2.1, 12.8], radius = 1.6 }, %)
    |> extrude(6, %)

  // Tread Detail
  sketch064 = startSketchOn(offsetPlane('YZ', width + 1.5))
    |> startProfileAt([-3.5, 3], %)
    |> angledLineToY({ angle = 65, to = 0.5 }, %, $seg10)
    |> xLine(6, %, $seg11)
    |> angledLineToY({
         angle = 180 - 65,
         to = profileStartY(%)
       }, %, $seg12)
    |> lineTo([profileStartX(%), profileStartY(%)], %, $seg13)
    |> close(%)
    |> extrude(4, %)
    |> fillet({
         radius = .2,
         tags = [
           getNextAdjacentEdge(seg10),
           getNextAdjacentEdge(seg11),
           getNextAdjacentEdge(seg12),
           getNextAdjacentEdge(seg13)
         ]
       }, %)
  sketch065 = startSketchOn(offsetPlane('YZ', width + 1.5))
    |> startProfileAt([-2, 4.7], %)
    |> angledLineToY({ angle = 50, to = 3.2 }, %, $seg14)
    |> xLine(3.2, %, $seg15)
    |> lineTo([profileStartX(%), profileStartY(%)], %, $seg16)
    |> close(%)
    |> hole(circle({ center = [-1.9, 4], radius = .25 }, %), %)
    |> extrude(4, %)
    |> fillet({
         radius = .7,
         tags = [getNextAdjacentEdge(seg16)]
       }, %)
  sketch066 = startSketchOn(offsetPlane('YZ', width + 2.5))
    |> startProfileAt([-4.7, 3.85], %)
    |> angledLine({ angle = 10 + 90, length = -.8 }, %)
    |> angledLine({ angle = 10, length = 8.7 }, %)
    |> yLine(1.2, %)
    |> lineTo([profileStartX(%), profileStartY(%)], %)
    |> close(%)
    |> hole(circle({ center = [-1.9, 4], radius = .25 }, %), %)
    |> extrude(2, %)
  return 0
}
tread(9.3)
tread(-16)

// Left Arm
sketch067 = startSketchOn(offsetPlane('YZ', 8.9))
  |> circle({ center = [-6.3, 17.35], radius = 0.5 }, %)
  |> extrude(2, %)

plane003 = {
  plane = {
    origin = [0, -6.3, 17.35],
    xAxis = [1, 0, 0],
    yAxis = [0, 1, -0.5],
    zAxis = [0, 0, 1]
  }
}

sketch068 = startSketchOn(plane003)
  |> circle({ center = [11.1, 0], radius = .5 }, %)
  |> extrude(1.5, %)
sketch069 = startSketchOn(sketch068, 'END')
  |> startProfileAt([12.1, -13.65], %)
  |> angledLine({ angle = -16 + 90, length = -1 }, %, $seg01A)
  |> angledLine({ angle = -16, length = -1 }, %, $seg02A)
  |> tangentialArcTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> hole(circle({ center = [11.1, -13.4], radius = .52 }, %), %)
  |> extrude(-1, %)
sketch070 = startSketchOn(sketch069, seg02A)
  |> startProfileAt([15.39, 14.2], %)
  |> yLine(-3, %, $seg07)
  |> xLine(-0.5, %)
  |> tangentialArcTo([
       profileStartX(%) - .5,
       profileStartY(%)
     ], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(6, %)

// Left Arm Detail
sketch071 = startSketchOn(sketch070, seg07)
  |> startProfileAt([14.19, 11.05], %)
  |> yLine(1.5, %)
  |> line([-1.77, -1.77], %)
  |> xLine(1.5, %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(.07, %)
sketch072 = startSketchOn(sketch070, seg07)
  |> startProfileAt([13.43, 17.81], %)
  |> xLine(0.75, %)
  |> yLine(-0.75, %)
  |> line([-3, -3], %)
  |> yLine(1.5, %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(.07, %)
sketch073 = startSketchOn(sketch070, seg07)
  |> startProfileAt([13.43 + .75, 17.81 - 3 + .75], %)
  |> yLine(-1.5, %)
  |> line([-3, -3], %)
  |> yLine(1.5, %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(.07, %)
sketch074 = startSketchOn(sketch070, 'END')
  |> startProfileAt([15.39, 14.2], %)
  |> yLine(-3, %)
  |> xLine(-0.6, %)
  |> tangentialArcTo([
       profileStartX(%) - .6,
       profileStartY(%)
     ], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(1, %)

// Left Arm Strut
sketch075 = startSketchOn(sketch074, 'END')
  |> circle({
       center = [14.35, 12.7],
       radius = 0.75
     }, %)
  |> extrude(3, %)
sketch076 = startSketchOn(sketch075, 'END')
  |> circle({
       center = [14.35, 12.7],
       radius = 0.65
     }, %)
  |> extrude(4, %)
sketch077 = startSketchOn(sketch069, 'START')
  |> circle({
       center = [-11.1, -13.36],
       radius = 0.99
     }, %)
  |> extrude(1, %)
sketch078 = startSketchOn(plane003)
  |> circle({ center = [11.1, 0], radius = .5 }, %)
  |> extrude(-1.5, %)
sketch079 = startSketchOn(sketch078, 'START')
  |> startProfileAt([-12.1, -13.65], %)
  |> angledLine({ angle = 16 + 90, length = -1 }, %)
  |> angledLine({ angle = 16, length = 1 }, %)
  |> tangentialArcTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> hole(circle({
       center = [-11.1, -13.4],
       radius = .52
     }, %), %)
  |> extrude(-1, %)

// Right Arm
sketch080 = startSketchOn(offsetPlane('YZ', -8.9))
  |> circle({ center = [-6.3, 17.35], radius = 0.5 }, %)
  |> extrude(-2, %)
plane004 = {
  plane = {
    origin = [0, -6.3, 17.35],
    xAxis = [1, 0, 0],
    yAxis = [0, 1, -0.45],
    zAxis = [0, 0, 1]
  }
}

sketch081 = startSketchOn(plane004)
  |> circle({ center = [-11.1, 0], radius = .5 }, %)
  |> extrude(1.5, %)
sketch082 = startSketchOn(plane004)
  |> circle({ center = [-11.1, 0], radius = .5 }, %)
  |> extrude(-1.5, %)
sketch083 = startSketchOn(sketch081, 'END')
  |> startProfileAt([-12.1, -13.12], %)
  |> angledLine({ angle = 16 + 90, length = -1 }, %, $seg01Ar)
  |> angledLine({ angle = 16, length = 1 }, %, $seg02Ar)
  |> tangentialArcTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> hole(circle({
       center = [-11.1, -12.87],
       radius = .52
     }, %), %)
  |> extrude(-1, %)
sketch084 = startSketchOn(sketch083, seg02Ar)
  |> startProfileAt([-15.26, 14.73], %)
  |> yLine(-3, %, $seg01Arr)
  |> xLine(0.5, %)
  |> tangentialArcTo([
       profileStartX(%) + .5,
       profileStartY(%)
     ], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(6, %)

// Right Arm Detail
sketch085 = startSketchOn(sketch084, seg01Arr)
  |> startProfileAt([-14.73, 11.98], %)
  |> yLine(-1.5, %)
  |> line([0.2, -0.2], %)
  |> xLine(1.5, %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(.07, %)
sketch086 = startSketchOn(sketch084, seg01Arr)
  |> startProfileAt([-14.75, 14.98], %)
  |> yLine(-1.5, %)
  |> line([3, -3], %)
  |> yLine(1.5, %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(.07, %)
sketch087 = startSketchOn(sketch084, seg01Arr)
  |> startProfileAt([-14.74, 17.3], %)
  |> yLine(-0.75, %)
  |> line([3, -3], %)
  |> yLine(1.5, %)
  |> lineTo([-14.74 + .75, 17.3], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(.07, %)
sketch088 = startSketchOn(sketch084, 'END')
  |> startProfileAt([-15.26, 14.73], %)
  |> yLine(-3, %)
  |> xLine(0.6, %)
  |> tangentialArcTo([
       profileStartX(%) + .6,
       profileStartY(%)
     ], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(1, %)

// Right Arm Strut
sketch089 = startSketchOn(sketch088, 'END')
  |> circle({
       center = [-14.2, 13.2],
       radius = 0.75
     }, %)
  |> extrude(3, %)
sketch090 = startSketchOn(sketch089, 'END')
  |> circle({
       center = [-14.2, 13.2],
       radius = 0.65
     }, %)
  |> extrude(4, %)
sketch091 = startSketchOn(sketch083, 'START')
  |> circle({
       center = [11.1, -12.87],
       radius = 0.99
     }, %)
  |> extrude(1, %)
sketch092 = startSketchOn(sketch082, 'start')
  |> startProfileAt([12.1, -13.12], %)
  |> angledLine({ angle = -16 - 90, length = 1 }, %)
  |> angledLine({ angle = -16, length = -1 }, %)
  |> tangentialArcTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> hole(circle({
       center = [11.1, -12.87],
       radius = .52
     }, %), %)
  |> extrude(-1, %)

// Right Hand
sketch093 = startSketchOn(plane004)
  |> circle({ center = [-7, -15], radius = 0.65 }, %)
  |> extrude(.7, %)
sketch094 = startSketchOn(sketch093, 'END')
  |> startProfileAt([1.08 - .3, -29.57], %)
  |> line([-3.99, -0.25], %)
  |> line([-3.93, 0.71], %)
  |> tangentialArcTo([-6.1 - .3, -26.98], %)
  |> tangentialArcTo([-5.14 - .3, -27.81], %)
  |> line([2.21, -0.8], %)
  |> line([3.97, -0.25], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> hole(circle({ center = [-7, -28], radius = 0.4 }, %), %)
  |> extrude(2.5, %)
sketch095 = startSketchOn(plane004)
  |> circle({ center = [-7, -15], radius = 0.65 }, %)
  |> extrude(-.7, %)
sketch096 = startSketchOn(sketch095, 'start')
  |> startProfileAt([-0.63 - 0.3, -26.83], %)
  |> line([-0.15, -0.78], %)
  |> line([3.78, -1.27], %)
  |> line([3.98, -0.48], %)
  |> tangentialArcTo([7.04 - 0.3, -26.92], %)
  |> tangentialArcTo([5.82 - 0.3, -27.58], %)
  |> xLine(-2.47, %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> hole(circle({ center = [7, -28], radius = 0.4 }, %), %)
  |> extrude(2.5, %)
sketch097 = startSketchOn(plane004)
  |> circle({ center = [-7, -15], radius = 0.1 }, %)
  |> extrude(1, %)
sketch098 = startSketchOn(sketch097, 'END')
  |> startProfileAt([-0.83, -26.58], %)
  |> line([-0.12, 0.65], %)
  |> xLine(-3.31, %)
  |> line([-2.96, -1.07], %)
  |> tangentialArcTo([-6.51, -28.85], %)
  |> tangentialArcTo([-5.72, -27.87], %)
  |> tangentialArcTo([-5.01, -27.04], %)
  |> line([1.21, 0.5], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(-2, %)

// Left Hand
sketch099 = startSketchOn(plane003)
  |> circle({
       center = [6.83, -14.83],
       radius = 0.65
     }, %)
  |> extrude(.7, %)
sketch100 = startSketchOn(sketch099, 'END')
  |> startProfileAt([-0.56 - .45, -28.33], %)
  |> line([-0.05, -0.65], %)
  |> line([3.95, -0.66], %)
  |> xLine(4, %)
  |> tangentialArcTo([6.22 - .45, -27.74], %)
  |> tangentialArcTo([5.61 - .45, -28.17], %)
  |> line([-2.52, -0.27], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> hole(circle({ center = [7, -28.3], radius = 0.4 }, %), %)
  |> extrude(2.5, %)
sketch101 = startSketchOn(plane003)
  |> circle({
       center = [6.83, -14.83],
       radius = 0.65
     }, %)
  |> extrude(-.7, %)
sketch102 = startSketchOn(sketch101, 'START')
  |> startProfileAt([0.99, -28.87], %)
  |> line([0.05, -0.6], %)
  |> line([-3.97, -0.57], %)
  |> line([-3.98, 0.37], %)
  |> tangentialArcTo([-5.94, -27.58], %)
  |> tangentialArcTo([-5.7, -27.87], %)
  |> tangentialArcTo([-5.05, -28.38], %)
  |> line([2.1, -0.6], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> hole(circle({ center = [-7, -28.3], radius = 0.4 }, %), %)
  |> extrude(2.5, %)
sketch103 = startSketchOn(plane003)
  |> circle({
       center = [6.83, -14.83],
       radius = 0.65
     }, %)
  |> extrude(1, %)
sketch104 = startSketchOn(sketch103, 'END')
  |> startProfileAt([0.86, -27.15], %)
  |> line([0.09, 0.61], %)
  |> xLine(3.52, %)
  |> line([2.6, -0.67], %)
  |> tangentialArcTo([5.81, -28.44], %)
  |> tangentialArcTo([4.67, -27.35], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> extrude(-2, %)
