// Gear
// A rotating machine part having cut teeth or, in the case of a cogwheel, inserted teeth (called cogs), which mesh with another toothed part to transmit torque. Geared devices can change the speed, torque, and direction of a power source. The two elements that define a gear are its circular shape and the teeth that are integrated into its outer edge, which are designed to fit into the teeth of another gear.

// Define constants
const gearWidth = 0.5
const toothScale = 0.33

// Create the body of the gear
const body = startSketchOn('YZ')
  |> circle('YZ', [0,0], 5* 0.33)
  |> hole(circle('YZ', [0,0], 1), %)
  |> extrude(gearWidth, %)

// Define the function for a single tooth
fn tooth = (toothSize) => {
  const singleTooth = startSketchOn('YZ')
  |> startProfileAt([
       0.0000000000 * toothSize,
       5.0000000000 * toothSize
     ], %)
  |> line([
       0.4900857016 * toothSize,
       -0.0240763666 * toothSize
     ], %)
  |> line([
       0.6804562304 * toothSize,
       0.9087880491 * toothSize
     ], %)
  |> line([
       0.5711661314 * toothSize,
       -0.1430696680 * toothSize
     ], %)
  |> line([
       0.1717090983 * toothSize,
       -1.1222443518 * toothSize
     ], %)
  |> line([
       0.4435665223 * toothSize,
       -0.2097913408 * toothSize
     ], %)
  |> close(%)
  |> extrude(gearWidth, %)
  return singleTooth
}

// Extrude the first tooth and pattern around the x-axis
const allGearTeeth = tooth(toothScale)
  |> patternCircular({
       arcDegrees: 360,
       axis: [1, 0],
       center: [0, 0, 0],
       repetitions: 16,
       rotateDuplicates: true
     }, %)