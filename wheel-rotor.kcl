// Rotor
// A rotor is a key component of the braking system, also known as a brake disc. The rotor is a flat, round piece of metal that is attached to the wheel hub, and it rotates with the wheel as the car moves. When you press the brake pedal, the brake pads clamp down on the rotor, creating friction that slows down the rotation of the wheel and, consequently, the car itself.

// Define constants
const rotorDiameter = 12 // inches
const rotorInnerDiameter = 6 // inches
const rotorSinglePlateThickness = 6.3 // mm
const rotorInnerDiameterThickness = 12.6 // mm
const lugHolePatternDia = 3 // inches

const rotorTotalThickness = 25.4 // mm
const spacerPatternDiameter = 11 // inches
const spacerDiameter = 6.3 // mm
const spacerLength = rotorTotalThickness - 2*rotorSinglePlateThickness
const spacerCount = 16
const wheeldiameter = 19 // inches
const lugSpacing = 114
const lugCount = 5

// Define the plane of the where the first side of the rotor will be
const rotorPlane = {
  plane: {
    origin: { x: 0, y: 10, z: 0 },
    xAxis: { x: -1, y: 0, z: 0 },
    yAxis: { x: 0, y: 0, z: 1 },
    zAxis: { x: 0, y: 1, z: 0 }
  }
}

// Define the lug hole pattern in a function
fn lugPattern = (plane) => {
  const lugHolePattern = circle([-lugSpacing/2, 0], 8, plane) 
  |> patternCircular2d({
    arcDegrees: 360,
    center: [0, 0],
    repetitions: lugCount - 1,
    rotateDuplicates: true,
  }, %)
  return lugHolePattern
}

// Draw the sketch of the first side of the rotor
const rotorSketch = startSketchOn(rotorPlane)
  |> circle([0, 0], rotorDiameter/2 * 25.4, %)
  |> hole(lugPattern(%), %)

// Extrude the rotor sketch
const rotor = extrude(rotorSinglePlateThickness, rotorSketch)

// Create a sketch for the bump on rotor disc
const rotorBumpSketch = startSketchOn(rotorPlane)
  |> circle([0, 0], rotorInnerDiameter/2 * 25.4, %)
  |> hole(lugPattern(%), %)

// Extrude the bump sketch
const rotorBump = extrude(-rotorInnerDiameterThickness, rotorBumpSketch)

// Define a plane for the other rotor disc
const rotorSecondaryPlatePlane = {
  plane: {
    origin: { x: 0, y: 10 + rotorTotalThickness*0.75, z: 0 },
    xAxis: { x: -1, y: 0, z: 0 },
    yAxis: { x: 0, y: 0, z: 1 },
    zAxis: { x: 0, y: 1, z: 0 }
  }
}

// Sketch the second rotor
const secondaryRotorSketch = startSketchOn(rotorSecondaryPlatePlane)
  |> circle([0, 0], rotorDiameter/2 * 25.4, %)
  |> hole(lugPattern(%), %)

// Extrude the second rotor
const secondRotor = extrude(rotorSinglePlateThickness, secondaryRotorSketch)

// Create a sketch for the spacers between the discs
const spacerSketch = startSketchOn(rotorSecondaryPlatePlane)
  |> circle([spacerPatternDiameter/2 * 25.4, 0], spacerDiameter, %)
  |> patternCircular2d({
    arcDegrees: 360,
    center: [0, 0],
    repetitions: spacerCount,
    rotateDuplicates: true,
  }, %)

// Extrude and pattern the spacers
const spacers = extrude(-spacerLength, spacerSketch)