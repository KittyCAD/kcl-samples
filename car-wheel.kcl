// Car Wheel
// A parametric example of a sports car wheel

// Define constants
const lugDia = 16
const lugCount = 5
const lugSpacing = 114.3
const baseThickness = 10
const offset = -35 // millimeters
const backSpacing = 6.38 // inches
const wheelWidth = 9.5 // inches
const wheelDiameter = 19 // inches

// Create the circular pattern for the lug holes
const circles = startSketchOn('XZ')
  |> circle([lugSpacing / 2, 0], lugDia / 2, %)
  |> patternCircular2d({
       arcDegrees: 360,
       center: [0, 0],
       repetitions: lugCount - 1,
       rotateDuplicates: true
     }, %)

// Create the wheel center and add lug holes
const flangeBase = startSketchOn('XZ')
  |> circle([0, 0], lugSpacing / 2 + lugDia + 5, %)
  |> hole(circles, %)
  |> extrude(baseThickness, %)

// Remove the interior of the wheel center
const sketch001 = startSketchOn(flangeBase, 'END')
  |> circle([0, 0], 20, %)
  |> extrude(-baseThickness, %)

// Add more material to the wheel center
const sketch002 = startSketchOn(flangeBase, 'END')
  |> circle([0, 0], lugSpacing / 2 + lugDia + 8, %)
  |> extrude(30, %)

// Edge treatment around center
const sketch003 = startSketchOn('XY')
  |> startProfileAt([
       lugSpacing / 2 + lugDia + 8,
       -baseThickness - 30
     ], %)
  |> yLine(30, %)
  |> line([6, -2], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> revolve({ axis: 'y' }, %)

// Center cap
const sketch004 = startSketchOn('XY')
  |> startProfileAt([15, -40], %)
  |> xLine(15, %)
  |> line([-3.01, -3.54], %)
  |> line([-8.8, 0.17], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> revolve({ axis: 'y' }, %)

// Holes for lug nut heads
const sketch005 = startSketchOn(sketch002, 'END')
  |> circle([lugSpacing / 2, 0], 17, %)
  |> patternCircular2d({
       arcDegrees: 360,
       center: [0, 0],
       repetitions: lugCount - 1,
       rotateDuplicates: true
     }, %)

const sketch006 = extrude(-30, sketch005)

// Seperating the spoke base planes
const plane001 = {
  plane: {
    origin: [0.0, 0.0, 5.0],
    x_axis: [1.0, 0.0, 0.0],
    y_axis: [0.0, 1.0, 0.0],
    z_axis: [0.0, 0.0, 1.0]
  }
}

const plane002 = {
  plane: {
    origin: [0.0, 0.0, -5.0],
    x_axis: [1.0, 0.0, 0.0],
    y_axis: [0.0, 1.0, 0.0],
    z_axis: [0.0, 0.0, 1.0]
  }
}

// Spoke cross sections
const sketch007 = startSketchOn(plane001)
  |> startProfileAt([
       lugSpacing / 2 + lugDia + 1,
       -baseThickness - 26
     ], %)
  |> bezierCurve({
       to: [
         wheelDiameter * 25.4 / 2 - (lugSpacing / 2) - lugDia - 23,
         12
       ],
       control1: [
         (wheelDiameter * 25.4 / 2 - (lugSpacing / 2) - lugDia - 23) / 2,
         0
       ],
       control2: [
         (wheelDiameter * 25.4 / 2 - (lugSpacing / 2) - lugDia - 23) / 2,
         15
       ]
     }, %)
  |> yLine(12, %)
  |> bezierCurve({
       to: [
         -wheelDiameter * 25.4 / 2 + lugSpacing / 2 + lugDia + 23,
         -12
       ],
       control1: [
         -(wheelDiameter * 25.4 / 2 - (lugSpacing / 2) - lugDia) / 2,
         0
       ],
       control2: [
         -(wheelDiameter * 25.4 / 2 - (lugSpacing / 2) - lugDia) / 2,
         -15
       ]
     }, %)
  |> close(%)

const sketch008 = startSketchOn(plane002)
  |> startProfileAt([
       lugSpacing / 2 + lugDia + 1,
       -baseThickness - 26
     ], %)
  |> bezierCurve({
       to: [
         wheelDiameter * 25.4 / 2 - (lugSpacing / 2) - lugDia - 23,
         12
       ],
       control1: [
         (wheelDiameter * 25.4 / 2 - (lugSpacing / 2) - lugDia - 23) / 2,
         0
       ],
       control2: [
         (wheelDiameter * 25.4 / 2 - (lugSpacing / 2) - lugDia - 23) / 2,
         15
       ]
     }, %)
  |> yLine(12, %)
  |> bezierCurve({
       to: [
         -wheelDiameter * 25.4 / 2 + lugSpacing / 2 + lugDia + 23,
         -12
       ],
       control1: [
         -(wheelDiameter * 25.4 / 2 - (lugSpacing / 2) - lugDia) / 2,
         0
       ],
       control2: [
         -(wheelDiameter * 25.4 / 2 - (lugSpacing / 2) - lugDia) / 2,
         -15
       ]
     }, %)
  |> close(%)

// Circular pattern spokes
const sketch009 = extrude(20, sketch007)
  |> patternCircular3d({
       axis: [0, 1, 0],
       center: [0, -2000, 0],
       repetitions: 5,
       arcDegrees: 360,
       rotateDuplicates: true
     }, %)

const sketch0010 = extrude(-20, sketch008)
  |> patternCircular3d({
       axis: [0, 1, 0],
       center: [0, -2000, 0],
       repetitions: 5,
       arcDegrees: 360,
       rotateDuplicates: true
     }, %)

// Wheel exterior
const sketch0011 = startSketchOn('XY')
  |> startProfileAt([
       wheelDiameter * 25.4 / 2,
       -wheelWidth * 25.4 + backSpacing * 25.4 + offset
     ], %)
  |> line([wheelWidth * 2, -wheelWidth], %)
  |> yLine(-wheelWidth * 2, %)
  |> xLine(-wheelWidth, %)
  |> yLine(wheelWidth * 1.5, %)
  |> line([-wheelWidth * 2, wheelWidth], %)
  |> yLine(wheelWidth * 2.7, %)
  |> line([-wheelWidth, wheelWidth], %)
  |> yLine(wheelWidth * 9, %)
  |> line([wheelWidth, wheelWidth], %)
  |> yLine(wheelWidth * 11, %)
  |> line([wheelWidth * 2, wheelWidth], %)
  |> yLine(wheelWidth * 2, %)
  |> xLine(wheelWidth, %)
  |> yLine(-wheelWidth * 2.7, %)
  |> line([-wheelWidth * 2, -wheelWidth], %)
  |> yLine(-wheelWidth * 11, %)
  |> line([-wheelWidth, -wheelWidth], %)
  |> yLine(-wheelWidth * 7.7, %)
  |> line([wheelWidth, -wheelWidth], %)
  |> lineTo([profileStartX(%), profileStartY(%)], %)
  |> close(%)
  |> revolve({ axis: 'y' }, %)
